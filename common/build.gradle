plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
}

architectury {
    common rootProject.enabled_platforms.split(',')
}

repositories {
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
    }
    maven { url 'https://maven.wispforest.io/releases' }
    maven { url 'https://maven.su5ed.dev/releases' }
    maven { url 'https://maven.fabricmc.net' }
    maven { url 'https://maven.shedaniel.me/' }
    flatDir {
        dirs("deps/local/com/bedrockk/molang/1.1.20")
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
}

dependencies {
    // We depend on Fabric Loader here to use the Fabric @Environment annotations,
    // which get remapped to the correct annotations on each platform.
    // Do NOT use other classes from Fabric Loader.
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "dev.architectury:architectury:$rootProject.architectury_api_version"

    modImplementation("com.cobblemon:mod:${project.property('cobblemon_version')}")


    implementation("org.graalvm.sdk:graal-sdk:22.3.0")

    // Test framework for common module
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"

    compileOnly("name:molang-1.1.20")

    modCompileOnlyApi("curse.maven:radical-cobblemon-trainers-api-1152792:7319958")

    // Forge Config API Port
    compileOnly "fuzs.forgeconfigapiport:forgeconfigapiport-common-neoforgeapi:$rootProject.fcap_version"

    //rctmod
    modCompileOnlyApi( "com.gitlab.srcmc:rctmod-common-1.21.1:0.17.4-beta")

    //accessories
    modCompileOnlyApi("io.wispforest:accessories-common:${project.accessories_version}")
    modCompileOnly("dragomordor.simpletms:simpletms-common:2.3.0")

    modCompileOnly("maven.modrinth:2iY8VFqL:y2ziXO9L")
}

// Default for all resource/copy tasks
tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// All archives (Jar/Zip/Shadow)
tasks.withType(AbstractArchiveTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// If you add per-file matches:
tasks.named("processResources").configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // Example:
    // filesMatching("META-INF/neoforge.mods.toml") { expand version: project.version }
}
